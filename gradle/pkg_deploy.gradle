apply plugin: 'maven-publish'
apply plugin: 'signing'

ext.sharedManifest = manifest {
    attributes 'Implementation-Vendor': group,
            'Implementation-Title': name,
            'Implementation-Version': version
}

jar {
    manifest {
        from sharedManifest
    }
}

// current project javadoc are written in ascidoc adn default java cannot process them
//task javadocJar(type: Jar, dependsOn: javadoc) {
//    archiveClassifier.set('javadoc')
//    from javadoc
//}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
    manifest {
        from sharedManifest
    }
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenCentral(MavenPublication) {
            customizePom(pom)

            from components.java

            artifact sourcesJar
            // artifact javadocJar
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask('publish') }
    sign publishing.publications.mavenCentral
}

void customizePom(pom) {
    pom.withXml {
        def root = asNode()

        def githubPage = 'https://github.com/katoquro/fg-net'

        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            name project.name
            description 'Fluent Groovy (and Java of course) network client'

            url githubPage

            organization {
                name 'Ainrif'
                url 'https://github.com/ainrif'
            }
            licenses {
                license {
                    name 'Apache License 2.0'
                    url "${githubPage}/blob/master/LICENSE"
                    distribution 'repo'
                }
            }
            scm {
                url githubPage
                connection "scm:git:${githubPage}.git"
                developerConnection 'scm:git:git@github.com:katoquro/fg-net.git'
            }
            issueManagement {
                system 'GitHub'
                url "${githubPage}/issues"
            }
            developers {
                developer {
                    name 'Andrew Malyhin'
                    email 'katoquro@gmail.com'
                }
            }
        }
    }
}
